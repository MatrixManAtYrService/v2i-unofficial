# TMX Plugin CMakeFile v. 2.2

CMAKE_MINIMUM_REQUIRED(VERSION @CMAKE_MINIMUM_REQUIRED_VERSION@)

PROJECT(@PLUGIN@ CXX)

SET(CMAKE_RUNTIME_OUTPUT_DIRECTORY bin)
SET(CPACK_PACKAGE_DIRECTORY "${CMAKE_BINARY_DIR}/bin")
SET(CMAKE_BINARY_DIR "${CMAKE_CURRENT_BINARY_DIR}")

# Read the version info from the manifest file
FILE(STRINGS manifest.json MANIFEST_DATA REGEX "\"version\":")
STRING(REGEX MATCH "([0-9]+)\\.?([0-9]+)?\\.?([0-9]+)?" PROJECT_VERSION "${MANIFEST_DATA}")
IF(DEFINED CMAKE_MATCH_1)
    SET(CPACK_PACKAGE_VERSION_MAJOR ${CMAKE_MATCH_1})
ENDIF()
IF(DEFINED CMAKE_MATCH_2)
    SET(CPACK_PACKAGE_VERSION_MINOR ${CMAKE_MATCH_2})
ENDIF()
IF(DEFINED CMAKE_MATCH_3)
    SET(CPACK_PACKAGE_VERSION_PATCH ${CMAKE_MATCH_3})
ENDIF()

# Plugins just ZIP the executable along with the manifest files
SET(CPACK_GENERATOR "ZIP")
SET(CPACK_PACKAGE_NAME ${PROJECT_NAME})
SET(CPACK_PACKAGE_VERSION "${CPACK_PACKAGE_VERSION_MAJOR}.${CPACK_PACKAGE_VERSION_MINOR}.${CPACK_PACKAGE_VERSION_PATCH}")
SET(CPACK_PACKAGE_FILE_NAME "${CPACK_PACKAGE_NAME}-${CPACK_PACKAGE_VERSION}")

INCLUDE(CPack)
    
FILE(GLOB_RECURSE SOURCES "src/*.c*")

SET(REQUIRE_XERCES_C @REQUIRE_XERCES_C@)
SET(REQUIRE_SNMP @REQUIRE_SNMP@)
SET(REQUIRE_CURL @REQUIRE_CURL@)

INCLUDE(PluginDependency)

ADD_EXECUTABLE( ${PROJECT_NAME} ${SOURCES} )
TARGET_LINK_LIBRARIES( ${PROJECT_NAME} ${Boost_LIBRARIES} pthread m rt
                       ${CMAKE_ARCHIVE_OUTPUT_DIRECTORY}/libivputils.a 
                       ${ivpapi_LIBRARY} ${asn_j2735_LIBRARY} )

IF (REQUIRE_XERCES_C)
    TARGET_LINK_LIBRARIES( ${PROJECT_NAME} ${XercesC_LIBRARY} )
ENDIF()
IF (REQUIRE_SNMP)
    TARGET_LINK_LIBRARIES( ${PROJECT_NAME} ${NETSNMP_LIBRARIES} )
ENDIF()
IF (REQUIRE_CULR)
    TARGET_LINK_LIBRARIES( ${PROJECT_NAME} ${CURL_LIBRARIES} )
ENDIF()

IF (CMAKE_VERSION VERSION_GREATER 3.3 AND CMAKE_VERSION VERSION_LESS 3.4)
    MESSAGE(STATUS "WARNING: CMake Version ${CMAKE_VERSION} contains bug 0015696, which prohibits the plugin packaging")
    MESSAGE(STATUS "         from executing correctly in this build.  You need to upgrade to CMake 3.4 or else package manually")
    MESSAGE(STATUS "         See https://cmake.org/Bug/view.php?id=15696 for more details")
    MESSAGE(WARNING "Unable to package the plugins directly from this build")
ELSE ()
    ADD_CUSTOM_TARGET(bundle_${PROJECT_NAME}
        COMMAND "${CMAKE_CPACK_COMMAND}" 
        "-C" "$<CONFIGURATION>" "--config" "${CMAKE_BINARY_DIR}/CPackConfig.cmake")
    ADD_CUSTOM_TARGET(bundle_${PROJECT_NAME}_cleanup
        COMMAND rm -rf "${CPACK_PACKAGE_DIRECTORY}/_CPack_Packages")
    ADD_DEPENDENCIES(bundle_${PROJECT_NAME}_cleanup bundle_${PROJECT_NAME})
    ADD_CUSTOM_COMMAND(TARGET ${PROJECT_NAME} POST_BUILD
                   COMMAND ${CMAKE_MAKE_PROGRAM} bundle_${PROJECT_NAME}_cleanup
                   WORKING_DIRECTORY ${CMAKE_BINARY_DIR} )
ENDIF ()

INSTALL(TARGETS ${PROJECT_NAME} DESTINATION bin)
INSTALL(FILES manifest.json DESTINATION .)
